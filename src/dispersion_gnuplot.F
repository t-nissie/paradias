! dispersion_gnuplot.F -*-f90-*-
! Time-stamp: <2013-02-12 19:14:02 takeshi>
! Author: Takeshi NISHIMATSU
!!
#include "define.h"

subroutine dispersion_gnuplot_xtics(xl, i, len)
  use Xtal_module
  implicit none
  type(Xtal_type), intent(in) :: xl
  integer i
  real*8 len
  character (len=2000), save :: xtics

  if (len.eq.0.0d0) then
     write(xtics,'(a,a,a)') "set xtics ('", TRIM(xl%k_points(0)%label), "' 0.0"
  else if (i.gt.MAX_AXIS) then
     write(xtics,'(a,a)') TRIM(xtics), ')'
     write(8,'(a)') TRIM(xtics)
  else
     write(xtics,'(a,a,a,a,f10.5)') TRIM(xtics), &
          & ", '", TRIM(xl%k_points(i)%label), "'", len
  end if
end subroutine dispersion_gnuplot_xtics

subroutine dispersion_gnuplot_open(xl)
  use Xtal_module
  implicit none
  type(Xtal_type) :: xl
  character (len=FILENAME_LEN) gnuplot_filename

  write(gnuplot_filename,'(2a)') TRIM(xl%filename), '.example.gp'
  open(8, FILE=gnuplot_filename, STATUS='REPLACE')

  write(8,'(a)') "#!/usr/bin/env gnuplot", &
       & "# This file is generated by paradias.", &
       & "##", &
       & "set key top left", &
       & "set grid", &
       & "set xlabel '{/Times-Italic k}'"
  write(8,'(3a)') 'set title "', TRIM(xl%title), '"'
  write(8,'(a)') "Y_MAX = 6.0", &
       & "Y_MIN = -4.0", &
       & "Y_AXIS = Y_MIN - (Y_MAX - Y_MIN)*0.0333333", &
       & "set yrange [Y_MIN:Y_MAX]", &
       & "set ytics 1"
  write(8,'(2a)') "set ylabel '(interaction energy per primitive cell)  /  ", &
       & "({/Times-Italic Z}^{*2}{/Times-Italic u}^2 / {/Symbol e_\245}{/Times-Italic a}^3)'"
  write(8,'(a)') "set terminal postscript landscape enhanced color dashed 'Times-Roman' 21"
  write(8,'(a,a,a)') "set output '", TRIM(xl%filename), ".eps'"
  call dispersion_gnuplot_xtics(xl,0,0.0d0)
end subroutine dispersion_gnuplot_open

subroutine dispersion_gnuplot_axis_label(xl, i, len)
  use Xtal_module
  implicit none
  type(Xtal_type) :: xl
  integer i
  real*8 len
  character (len=200), save :: labels(MAX_AXIS)
  integer,       save :: max_i
  integer j

  if (i.gt.MAX_AXIS) then
     write(8,'(a)') (TRIM(labels(j)),j=1,max_i)
  else
     write(labels(i),'(a,i4,a,a,a,f10.5,a)') &
          & 'set label ', 1000+i, " '", TRIM(xl%axes(i)%label), &
          & "' at ", len, ', Y_AXIS center font "Times-Roman,18"'
     max_i = i
  end if
end subroutine dispersion_gnuplot_axis_label

subroutine dispersion_gnuplot_close(xl,eigenvectors)
  use Xtal_module
  implicit none
  type(Xtal_type) :: xl
  complex*16 eigenvectors(3*xl%n_atoms,3*xl%n_atoms)
  real*8 absz(3*xl%n_atoms)   ! auto allocation
  integer i,j

  call dispersion_gnuplot_xtics(xl,999999,100.0d0)
  call dispersion_gnuplot_axis_label(xl,999999,100.0d0)


  write(8,'(a)') 'plot \'
  do j = 1, 3*xl%n_atoms
     absz(j) = 0.0d0
     do i = 1, xl%n_atoms
        absz(j) = absz(j) + abs(eigenvectors(i*3,j))
     end do
     write(8,'(3a,i2,a)') "   '", TRIM(xl%filename), ".dispersion' using 1:($", j+4, "/2) notitle w l lt 1 lw 2,\"
  end do
  write(8,'(3a,$)')       "   '", TRIM(xl%filename), ".dispersion' using 1:(("
  do j = 1, 3*xl%n_atoms
     write(8,'(a,i2,a,$)') '$', j+4, ' + '
  end do
  write(8,'(a)') "0)/2) title 'sum rule' w p lt 4 lw 1"

  write(8,'(a)') 'set output'
  write(8,'(a)') 'set nolabel'
  close(8)
end subroutine dispersion_gnuplot_close

!Local variables:
!  compile-command: "make -k"
!End:
